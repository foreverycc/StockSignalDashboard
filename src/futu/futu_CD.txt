{S := INPUT(12, TITLE='SHORT EMA PERIOD') P := INPUT(26, TITLE='LONG EMA PERIOD') M := INPUT(9, TITLE='SIGNAL LINE PERIOD');}

{计算DIFF, DEA和MACD值}
FASTEMA := EMA(CLOSE, 12);
SLOWEMA := EMA(CLOSE, 26);
DIFF := FASTEMA - SLOWEMA;
DEA := EMA(DIFF, 9);
MCD := (DIFF - DEA) * 2;

{计算N1和MM1}
N1 := BARSLAST(CROSS(0, MCD));
MM1 := BARSLAST(CROSS(MCD, 0));

{确保长度参数大于0}
N1_SAFE := N1 + 1;
MM1_SAFE := MM1 + 1;

{计算CC和DIFL系列值}
CC1 := LLV(CLOSE, N1_SAFE);
CC2 := REF(CC1,MM1_SAFE);
CC3 := REF(CC2,MM1_SAFE);
DIFL1 := LLV(DIFF, N1_SAFE);
DIFL2 := REF(DIFL1,MM1_SAFE);
DIFL3 := REF(DIFL2,MM1_SAFE);

{计算CH和DIFH系列值}
CH1 := HHV(CLOSE, MM1_SAFE);
CH2 := REF(CH1,N1_SAFE);
CH3 := REF(CH2,N1_SAFE);
DIFH1 := HHV(DIFF, MM1_SAFE)
DIFH2 := REF(DIFH1,N1_SAFE);
DIFH3 := REF(DIFH2,N1_SAFE);

{判断买入条件}
AAA := CC1 < CC2 AND DIFL1 > DIFL2 AND REF(MCD,1) < 0 AND DIFF < 0;
BBB := CC1 < CC3 AND DIFL1 < DIFL2 AND DIFL1 > DIFL3 AND REF(MCD,1) < 0 AND DIFF < 0;
CCC := (AAA OR BBB) AND DIFF < 0;
JJJ := REF(CCC,1) AND ABS(REF(DIFF,1)) >= ABS(DIFF) * 1.01;
DXDX := NOT(REF(JJJ,1)) AND JJJ;

{判断卖出条件}
ZJDBL := CH1 > CH2 AND DIFH1 < DIFH2 AND REF(MCD,1) > 0 AND DIFF > 0;
GXDBL := CH1 > CH3 AND DIFH1 > DIFH2 AND DIFH1 < DIFH3 AND REF(MCD,1) > 0 AND DIFF > 0;
DBBL := (ZJDBL OR GXDBL) AND DIFF > 0;
DBJG := REF(DBBL,1) AND REF(DIFF,1)>= DIFF * 1.01;
DBJGXC := NOT(REF(DBJG,1)) AND DBJG;

{信号高低位置}
MATR:=XMA(TR,144); {用XMA只是为了更好的决定标记的高低，逻辑和信号不引用此变量}
VARPU:=H+MATR*0.3;
VARPD:=L-MATR*0.3;

{PLOT买入和卖出信号}
DRAWTEXT(DXDX,VARPD,'抄底'),COLORF6465D;  
DRAWTEXT(DBJGXC,VARPU,'卖出'),COLOR0ECB81;